plugins {
    id 'com.android.application'
    id 'com.github.triplet.play' version '2.5.0'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.google.gms.oss.licenses.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'org.jlleitschuh.gradle.ktlint'
}

apply from: 'tasks.gradle' // contains supporting tasks e.g ktlint
apply from: 'release-sign-credentials.gradle' // handles signing of release version
apply from: 'play-store-publisher-config.gradle' // handles Play Store releases
apply from: 'sentry-config.gradle' // handles Sentry bug tracking config

android {

    compileSdkVersion 29

    defaultConfig {
        applicationId "at.ict4d.ict4dnews"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode rootProject.ext.versionCode
        versionName "${rootProject.ext.versionMajor}.${rootProject.ext.versionMinor}.${rootProject.ext.versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testOptions.unitTests.includeAndroidResources = true

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/room_schemas".toString())
            }
        }

        compileOptions {
            sourceCompatibility 1.8
            targetCompatibility 1.8
        }

        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    signingConfigs {
        release {
            storeFile file(releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeyStorePath, 'fillhere'))
            storePassword releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeystorePw)
            keyAlias releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeyAlias)
            keyPassword releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeyPw)
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false

            buildConfigField "String", "SENTRY_DNS", "\"${sentryConfig.props.getProperty(sentryConfig.keySentrySecret)}\""
        }

        debug {
            minifyEnabled false
            versionNameSuffix "-debug"
            versionNameSuffix " (${getCheckedOutGitCommitHash()}.${rootProject.ext.versionCode})-debug"

            buildConfigField "String", "SENTRY_DNS", "\"${sentryConfig.props.getProperty(sentryConfig.keySentrySecret)}\""
        }
    }

    androidExtensions {
        experimental = true
    }

    dataBinding {
        enabled = true
    }

    ktlint {
        android = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    android {
        lintOptions {
            abortOnError false
        }
    }
}

// config to publish app bundle to the Play Store
play {
    serviceAccountCredentials = file(playStorePublisherConfig.props.getProperty(playStorePublisherConfig.keyPlayStoreJsonFilePath, 'this_should_not_happen.json'))
    track = "internal"
    userFraction = 1
    releaseStatus = "draft"
    defaultToAppBundles = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Android Support Libs
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "com.google.android.material:material:1.1.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta4"
    implementation "androidx.vectordrawable:vectordrawable:1.1.0"
    implementation "androidx.preference:preference:1.1.1"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.browser:browser:1.2.0"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.core:core-ktx:1.2.0"

    // Play Services
    implementation "com.google.android.gms:play-services-auth:18.0.0"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko-common:0.10.8"

    // Logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:2.2.0"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version@aar"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:3.12.2" // do not update since OkHttp 3.13+ requires API 21+
    implementation "com.squareup.retrofit2:converter-simplexml:2.6.2"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // ThreeTenABP - java.time backport
    implementation "com.jakewharton.threetenabp:threetenabp:1.2.3"

    // *** Android Architecture Components ***

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:2.1.0" // Test helpers for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version" // ReactiveStreams support for LiveData

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Test helpers for Room
    testImplementation "androidx.room:room-testing:$room_version"

    // RxJava support for Room
    implementation "androidx.room:room-rxjava2:$room_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Stetho for debuging
    implementation "com.facebook.stetho:stetho:1.5.1"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.1"

    // Testing
    testImplementation "junit:junit:4.13"
    androidTestImplementation "androidx.test:runner:1.2.0"
    testImplementation "androidx.test:core:1.2.0"
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation "androidx.annotation:annotation:1.1.0"
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation "org.robolectric:multidex:3.4.2"

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.2"
    implementation "com.squareup.leakcanary:leakcanary-object-watcher-android:2.2"

    // 3rd Party licenses
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"

    // Rx Preferences
    implementation "com.f2prateek.rx.preferences2:rx-preferences:2.0.0"

    // Jsoup
    implementation "org.jsoup:jsoup:1.13.1"

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.paging:paging-rxjava2-ktx:$paging_version"
    testImplementation "androidx.paging:paging-common-ktx:$paging_version"

    // Scrolling Indicators
    implementation "com.simplecityapps:recyclerview-fastscroll:2.0.1"

    // Sentry Bug-Tracker
    implementation "io.sentry:sentry-android:$sentry_version"

    // Koin AndroidX ViewModel features
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    // Android Debug Database
    debugImplementation "com.amitshekhar.android:debug-db:1.0.6"
}

configurations {
    all {
        exclude group: 'xpp3', module: 'xpp3'
    }
}