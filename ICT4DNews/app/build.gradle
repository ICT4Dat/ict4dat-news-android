plugins {
    id 'com.android.application'
    id 'com.github.triplet.play' version '2.2.0'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.google.gms.oss.licenses.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

apply from: 'tasks.gradle' // contains supporting tasks e.g ktlint
apply from: 'release-sign-credentials.gradle' // handles signing of release version
apply from: 'play-store-publisher-config.gradle' // handles Play Store releases
apply from: 'sentry-config.gradle' // handles Play Store releases

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "at.ict4d.ict4dnews"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode rootProject.ext.versionCode
        versionName "${rootProject.ext.versionMajor}.${rootProject.ext.versionMinor}.${rootProject.ext.versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testOptions.unitTests.includeAndroidResources = true

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/room_schemas".toString())
            }
        }

        compileOptions {
            sourceCompatibility 1.8
            targetCompatibility 1.8
        }
    }

    signingConfigs {
        release {
            storeFile file(releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeyStorePath, 'fillhere'))
            storePassword releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeystorePw)
            keyAlias releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeyAlias)
            keyPassword releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeyPw)
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false

            buildConfigField "String", "SENTRY_DNS", "\"${sentryConfig.props.getProperty(sentryConfig.keySentrySecret)}\""
        }

        debug {
            minifyEnabled false
            versionNameSuffix "-debug"
            versionNameSuffix " (${getCheckedOutGitCommitHash()}.${rootProject.ext.versionCode})-debug"

            buildConfigField "String", "SENTRY_DNS", "\"${sentryConfig.props.getProperty(sentryConfig.keySentrySecret)}\""
        }
    }

    androidExtensions {
        experimental = true
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    android {
        lintOptions {
            abortOnError false
        }
    }
}

// Exclude LeakCanary in tests
// Ensure the no-op dependency is always used in JVM tests.
configurations.all { config ->
    if (config.name.contains('UnitTest') || config.name.contains('AndroidTest')) {
        config.resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
            }
        }
    }
}

// config to publish app bundle to the Play Store
play {
    serviceAccountCredentials = file(playStorePublisherConfig.props.getProperty(playStorePublisherConfig.keyPlayStoreJsonFilePath, 'this_should_not_happen.json'))
    track = "internal"
    userFraction = 1
    releaseStatus = "draft"
    defaultToAppBundles = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Android Support Libs
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha4"
    implementation "androidx.vectordrawable:vectordrawable:1.0.1"
    implementation "androidx.preference:preference:1.0.0"
    implementation "androidx.annotation:annotation:1.0.2"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.browser:browser:1.0.0"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.core:core-ktx:1.0.1"

    // Play Services
    implementation "com.google.android.gms:play-services-auth:16.0.1"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko-common:0.10.8"

    // Logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.7"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:2.2.0"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version@aar"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:3.12.2" // do not update since OkHttp 3.13+ requires API 21+
    implementation "com.squareup.retrofit2:converter-simplexml:2.3.0"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // ThreeTenABP - java.time backport
    implementation "com.jakewharton.threetenabp:threetenabp:1.2.0"

    // *** Android Architecture Components ***

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:$lifecycle_version" // Test helpers for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version" // ReactiveStreams support for LiveData

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Test helpers for Room
    testImplementation "androidx.room:room-testing:$room_version"

    // RxJava support for Room
    implementation "androidx.room:room-rxjava2:$room_version"

    // Navigation
    implementation "android.arch.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$navigation_version"

    // Stetho for debuging
    implementation "com.facebook.stetho:stetho:1.5.1"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.1"

    // Testing
    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test:runner:1.1.1"
    testImplementation "androidx.test:core:1.1.0"
    androidTestImplementation "androidx.test:rules:1.1.1"
    androidTestImplementation "androidx.annotation:annotation:1.0.2"
    testImplementation "org.robolectric:robolectric:4.2.1"
    testImplementation "org.robolectric:multidex:3.4.2"

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:1.6.3"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.6.3"

    //KLint
    ktlint "com.github.shyiko:ktlint:0.31.0"

    // 3rd Party licenses
    implementation "com.google.android.gms:play-services-oss-licenses:16.0.2"

    // Rx Preferences
    implementation "com.f2prateek.rx.preferences2:rx-preferences:2.0.0"

    // Jsoup
    implementation "org.jsoup:jsoup:1.11.3"

    // WorkManager
    implementation "android.arch.work:work-runtime-ktx:$work_version"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.paging:paging-rxjava2-ktx:$paging_version"
    testImplementation "androidx.paging:paging-common-ktx:$paging_version"

    // Scrolling Indicators
    implementation "com.simplecityapps:recyclerview-fastscroll:2.0.0"

    // Sentry Bug-Tracker
    implementation "io.sentry:sentry-android:$sentry_version"

    // Koin for Android
    implementation "org.koin:koin-android:$koin_version"

    // Koin AndroidX ViewModel features
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
}

configurations {
    all {
        exclude group: 'xpp3', module: 'xpp3'
    }
}