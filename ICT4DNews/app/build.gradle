import com.github.triplet.gradle.androidpublisher.ReleaseStatus

plugins {
    id 'com.android.application'
    id 'com.github.triplet.play' version '3.8.6'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp' version '1.9.10-1.0.13'
    id 'kotlin-parcelize'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'org.jlleitschuh.gradle.ktlint'
    id "io.sentry.android.gradle" version '4.1.0'
}

apply from: 'tasks.gradle' // contains supporting tasks e.g ktlint
apply from: 'release-sign-credentials.gradle' // handles signing of release version
apply from: 'play-store-publisher-config.gradle' // handles Play Store releases
apply from: 'sentry-config.gradle' // handles Sentry bug tracking config

android {

    compileSdk 34
    namespace 'at.ict4d.ict4dnews'

    defaultConfig {
        applicationId "at.ict4d.ict4dnews"
        minSdkVersion 21
        targetSdk 34
        versionCode rootProject.ext.versionCode
        versionName "${rootProject.ext.versionMajor}.${rootProject.ext.versionMinor}.${rootProject.ext.versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testOptions.unitTests.includeAndroidResources = true

        ksp {
            arg(new RoomSchemaArgProvider(new File(projectDir, "room_schemas")))
        }

        compileOptions {

            // Flag to enable support for the new language APIs
            coreLibraryDesugaringEnabled true

            // Sets Java compatibility to Java 11
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }

        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_11.toString()
        }
    }

    signingConfigs {
        release {
            storeFile file(releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeyStorePath, 'fillhere'))
            storePassword releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeystorePw)
            keyAlias releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeyAlias)
            keyPassword releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeyPw)
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false

            resValue "string", "sentry_dns", "\"${sentryConfig.props.getProperty(sentryConfig.keySentrySecret)}\""
        }

        debug {
            minifyEnabled false
            versionNameSuffix "-debug"
            versionNameSuffix " (${getCheckedOutGitCommitHash()}.${rootProject.ext.versionCode})-debug"

            resValue "string", "sentry_dns", "\"${sentryConfig.props.getProperty(sentryConfig.keySentrySecret)}\""
        }
    }

    ktlint {
        android = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    buildFeatures {
        dataBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.3"
    }

    android {
        lintOptions {
            abortOnError false
        }
    }
}

// config to publish app bundle to the Play Store
play {
    serviceAccountCredentials.set(file(playStorePublisherConfig.props.getProperty(playStorePublisherConfig.keyPlayStoreJsonFilePath, 'this_should_not_happen.json')))
    track.set("internal")
    userFraction.set(1.0.toDouble())
    releaseStatus.set(ReleaseStatus.DRAFT)
    defaultToAppBundles.set(true)
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Android Support Libs
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.vectordrawable:vectordrawable:1.1.0"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.annotation:annotation:1.7.1"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.browser:browser:1.7.0"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.core:core-ktx:1.12.0"
    implementation 'com.google.android.material:material:1.11.0'

    // Compose
    def composeBom = platform('androidx.compose:compose-bom:2023.10.01')
    implementation composeBom
    androidTestImplementation composeBom
    implementation 'androidx.compose.material3:material3'
    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    // Optional - Add window size utils
    implementation 'androidx.compose.material3:material3-window-size-class'
    // Optional - Integration with activities
    implementation 'androidx.activity:activity-compose:1.8.2'
    // Optional - Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.2'
    // Optional - Integration with LiveData
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation "com.google.accompanist:accompanist-permissions:0.33.1-alpha"

    // Play Services
    implementation "com.google.android.gms:play-services-auth:20.7.0"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Logging
    implementation "com.jakewharton.timber:timber:5.0.1"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    ksp "com.github.bumptech.glide:ksp:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version@aar"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"
    implementation "com.squareup.retrofit2:converter-simplexml:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // *** Android Architecture Components ***

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:2.2.0" // Test helpers for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version" // ReactiveStreams support for LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Testing
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test:runner:1.5.2"
    testImplementation "androidx.test:core:1.5.0"
    androidTestImplementation "androidx.test:rules:1.5.0"
    androidTestImplementation "androidx.annotation:annotation:1.7.1"
    testImplementation "org.robolectric:robolectric:4.10.2"
    testImplementation "org.robolectric:multidex:3.4.2"

    // 3rd Party licenses
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.1"

    // Flow Preferences
    implementation "com.github.tfcporciuncula.flow-preferences:flow-preferences:1.3.2"

    // Jsoup
    implementation "org.jsoup:jsoup:1.15.4"

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Sentry Bug-Tracker
    implementation "io.sentry:sentry-android-timber:$sentry_version"

    // Koin AndroidX ViewModel features
    implementation "io.insert-koin:koin-android:$koin_version"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.4"
}

configurations {
    configureEach {
        exclude group: 'xpp3', module: 'xpp3'
    }
}

class RoomSchemaArgProvider implements CommandLineArgumentProvider {

    @InputDirectory
    @PathSensitive(PathSensitivity.RELATIVE)
    File schemaDir

    RoomSchemaArgProvider(File schemaDir) {
        this.schemaDir = schemaDir
    }

    @Override
    Iterable<String> asArguments() {
        // Note: If you're using KSP, change the line below to return
        // ["room.schemaLocation=${schemaDir.path}".toString()].
        return ["room.schemaLocation=${schemaDir.path}".toString()]
    }
}