import com.github.triplet.gradle.androidpublisher.*

plugins {
    id 'com.android.application'
    id 'com.github.triplet.play' version '3.0.0'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.google.gms.oss.licenses.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'org.jlleitschuh.gradle.ktlint'
}

apply from: 'tasks.gradle' // contains supporting tasks e.g ktlint
apply from: 'release-sign-credentials.gradle' // handles signing of release version
apply from: 'play-store-publisher-config.gradle' // handles Play Store releases
apply from: 'sentry-config.gradle' // handles Sentry bug tracking config

android {

    compileSdkVersion 30

    defaultConfig {
        applicationId "at.ict4d.ict4dnews"
        minSdkVersion 21
        targetSdkVersion 30
        buildToolsVersion "30.0.2"
        versionCode rootProject.ext.versionCode
        versionName "${rootProject.ext.versionMajor}.${rootProject.ext.versionMinor}.${rootProject.ext.versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testOptions.unitTests.includeAndroidResources = true

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/room_schemas".toString())
            }
        }

        compileOptions {

            // Flag to enable support for the new language APIs
            coreLibraryDesugaringEnabled true

            // Sets Java compatibility to Java 8
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    signingConfigs {
        release {
            storeFile file(releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeyStorePath, 'fillhere'))
            storePassword releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeystorePw)
            keyAlias releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeyAlias)
            keyPassword releaseSignCredLogic.props.getProperty(releaseSignCredLogic.configKeyPw)
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false

            resValue "string", "sentry_dns", "\"${sentryConfig.props.getProperty(sentryConfig.keySentrySecret)}\""
        }

        debug {
            minifyEnabled false
            versionNameSuffix "-debug"
            versionNameSuffix " (${getCheckedOutGitCommitHash()}.${rootProject.ext.versionCode})-debug"

            resValue "string", "sentry_dns", "\"${sentryConfig.props.getProperty(sentryConfig.keySentrySecret)}\""
        }
    }

    androidExtensions {
        experimental = true
    }

    android.buildFeatures.dataBinding = true

    ktlint {
        android = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    android {
        lintOptions {
            abortOnError false
        }
    }
}

// config to publish app bundle to the Play Store
play {
    serviceAccountCredentials.set(file(playStorePublisherConfig.props.getProperty(playStorePublisherConfig.keyPlayStoreJsonFilePath, 'this_should_not_happen.json')))
    track.set("internal")
    userFraction.set(1.0.toDouble())
    releaseStatus.set(ReleaseStatus.DRAFT)
    defaultToAppBundles.set(true)
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Android Support Libs
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "com.google.android.material:material:1.2.1"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.vectordrawable:vectordrawable:1.1.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.browser:browser:1.3.0"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.core:core-ktx:1.3.2"

    // Play Services
    implementation "com.google.android.gms:play-services-auth:19.0.0"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko-common:0.10.8"

    // Logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version@aar"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"
    implementation "com.squareup.retrofit2:converter-simplexml:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // *** Android Architecture Components ***

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:2.1.0" // Test helpers for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version" // ReactiveStreams support for LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Stetho for debuging
    implementation "com.facebook.stetho:stetho:1.5.1"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.1"

    // Testing
    testImplementation "junit:junit:4.13.1"
    androidTestImplementation "androidx.test:runner:1.3.0"
    testImplementation "androidx.test:core:1.3.0"
    androidTestImplementation "androidx.test:rules:1.3.0"
    androidTestImplementation "androidx.annotation:annotation:1.1.0"
    testImplementation "org.robolectric:robolectric:4.4"
    testImplementation "org.robolectric:multidex:3.4.2"

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.5"
    implementation "com.squareup.leakcanary:leakcanary-object-watcher-android:2.5"

    // 3rd Party licenses
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"

    // Flow Preferences
    implementation "com.github.tfcporciuncula.flow-preferences:flow-preferences:1.3.2"

    // Jsoup
    implementation "org.jsoup:jsoup:1.13.1"

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Scrolling Indicators
    implementation "com.simplecityapps:recyclerview-fastscroll:2.0.1"

    // Sentry Bug-Tracker
    implementation "io.sentry:sentry-android:$sentry_version"
    implementation "io.sentry:sentry-android-timber:$sentry_version"

    // Koin AndroidX ViewModel features
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.1"
}

configurations {
    all {
        exclude group: 'xpp3', module: 'xpp3'
    }
}